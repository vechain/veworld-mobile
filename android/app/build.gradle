apply plugin: "com.android.application"
apply plugin: "com.facebook.react"
apply plugin: "kotlin-android"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def debugKeyFile = file('debug.keystore')
def debugPublicKey = "\"DEBUG_KEY_NOT_FOUND\""

if (debugKeyFile.exists()) {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'keytool',
                '-exportcert',
                '-alias', 'androiddebugkey',
                '-keystore', debugKeyFile,
                '-storepass', 'android',
                '-rfc'
        standardOutput = stdout
    }

    def certContent = stdout.toString().trim()
            .replaceAll("-----BEGIN CERTIFICATE-----", "")
            .replaceAll("-----END CERTIFICATE-----", "")
            .replaceAll("\\s+", "")
    debugPublicKey = "\"${certContent}\""
} else {
    println "Debug keystore not found. Using placeholder."
}

/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
}

def enableProguardInReleaseBuilds = false

project.ext.vectoricons = [
        iconFontNames: ['MaterialIcons.ttf', 'MaterialCommunityIcons.ttf'] // Name of the font files you want to copy
]

apply from: file("../../node_modules/react-native-vector-icons/fonts.gradle")
apply from: "../../node_modules/@sentry/react-native/sentry.gradle"

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = 'org.webkit:android-jsc-intl:+'`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'org.webkit:android-jsc:+'

android {
    ndkVersion rootProject.ext.ndkVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    namespace "org.vechain.veworld.app"

    defaultConfig {
        applicationId "org.vechain.veworld.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 276
        versionName "2.2.0"
        testBuildType System.getProperty('testBuildType', 'debug')
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    // Build Types
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField "String", "PUBLIC_KEY", debugPublicKey
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            proguardFile "${rootProject.projectDir}/../node_modules/detox/android/detox/proguard-rules-app.pro"
            buildConfigField "String", "PUBLIC_KEY", System.getenv('PUBLIC_KEY') ?: (keystoreProperties['publicKey'] ?: "null")
        }
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
        pickFirst 'lib/x86/libcrypto.so'
        pickFirst 'lib/x86_64/libcrypto.so'
        pickFirst 'lib/armeabi-v7a/libcrypto.so'
        pickFirst 'lib/arm64-v8a/libcrypto.so'
    }

    applicationVariants.all { variant ->
        if (project.tasks.findByName("minify${variant.name.capitalize()}WithR8")) {
            tasks["minify${variant.name.capitalize()}WithR8"].finalizedBy { tasks["uploadMapping${variant.name.capitalize()}"] }
        }
    }
}

dependencies {
    androidTestImplementation('com.wix:detox:+')
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0"
    implementation 'com.google.android.gms:play-services-base:18.5.0'
    implementation 'com.google.android.gms:play-services-auth:21.2.0'
    implementation('com.google.apis:google-api-services-drive:v3-rev20221023-2.0.0') {
        exclude group: 'com.google.guava', module: "guava"
    }
    implementation 'com.google.guava:guava:31.0.1-android'
    implementation 'com.google.api-client:google-api-client-android:2.6.0'
    implementation 'com.google.api-client:google-api-client-gson:1.34.0'
    
    debugImplementation("com.facebook.flipper:flipper:${FLIPPER_VERSION}")
    debugImplementation("com.facebook.flipper:flipper-network-plugin:${FLIPPER_VERSION}") {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    debugImplementation("com.facebook.flipper:flipper-fresco-plugin:${FLIPPER_VERSION}")

    // Hermes
    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle")
applyNativeModulesAppBuildGradle(project)
